;; 2. Description: PK model 1 cmt base
;; x1. Author: Will Hane
;; Based on: run322.mod

$PROBLEM PK model 1 cmt base with weight effect

$INPUT ID TIME MDV EVID DV AMT  SEX WT ETN TRT AGE SMOKING DIABETES BMI BSA CLCR
$DATA acop2.csv IGNORE=@
$SUBROUTINES ADVAN2 TRANS2

$PK
ET=1
IF(ETN.EQ.3) ET=1.3
KA = THETA(1) + THETA(8)*SEX
CL = THETA(2)*(WT/70)**THETA(6)*EXP(ETA(1))
V = THETA(3)*(WT/70)**THETA(7)*EXP(ETA(2))
SC=V

$THETA
(0, 2)  ; THETA1: Absorption rate constant
(0, 20)  ; THETA2: Clearance
(0, 100) ; THETA3: Central Volume
(0.02)  ; RUVp
(1)     ; RUVa
(0.75) ; THETA6: Body weight effect on CL
(1.0) ; THETA7: Body weight effect on V
(0.1) ; THETA8: Sex effect on KA

$OMEGA
0.05    ; Random variability for CL
0.2     ; Random variability for V

$SIGMA
1 FIX

$ERROR
IPRED = F
IRES = DV-IPRED
W = IPRED*THETA(4) + THETA(5)
IF (W.EQ.0) W = 1
IWRES = IRES/W ; Weighted residual error
Y= IPRED+W*ERR(1)

$EST METHOD=1 INTERACTION MAXEVAL=99999 NSIG=3 PRINT=5 NOABORT POSTHOC MSFO=msf326
$COV PRINT=E
;$SIM (1234) NSUBPROBLEMS=1 ONLYSIM

$TABLE ID TIME DV MDV EVID IPRED IRES IWRES CPRED CWRES CPREDI CWRESI ETA1 ETA2 ONEHEADER NOPRINT FILE=sdtab326
$TABLE ID CL V ONEHEADER NOPRINT FILE=patab326
$TABLE ID SEX ETN ONEHEADER NOPRINT FILE=catab326
$TABLE ID WT ONEHEADER NOPRINT FILE=cotab326
$TABLE ID CL V SEX ETN WT TRT AGE SMOKING DIABETES BMI BSA CLCR ONEHEADER NOPRINT FILE=mytab326
